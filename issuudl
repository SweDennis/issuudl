#! /usr/bin/gawk -f

BEGIN {
    url      =  ARGV[1]
    UA       = "'Mozilla/5.0 (Windows NT 10.0; Win64; x64; rv:124.0) Gecko/20100101 Firefox/124.0'"
    cmd      = "curl -s -A "UA" "url
    matchPat = @/pageCount\\":([[:digit:]]+),\\"publicationId\\":\\"([[:alnum:]]+)\\",\\"revisionId\\":\\"([[:digit:]]+)\\",\\"title\\":\\"([[:alnum:][:blank:],]+)/

    while(cmd | getline) {
        if(match($0, matchPat, resArray)) {
           pageCount     = resArray[1]
           publicationId = resArray[2]
           revisionId    = resArray[3]
           title         = resArray[4]
        }
    }
    "mktemp -d" | getline tmpDir 
    for(i = 1; i <= pageCount; i++) {
        image = "https://image.isu.pub/"revisionId"-"publicationId"/jpg/page_"i".jpg"
        system("curl -s -A "UA" --output-dir "tmpDir" -O "image)
    }

    conCmd = "convert -quality 70"
    for(i = 1; i <= pageCount; i++) {
        "file "tmpDir"/page_"i".jpg" | getline fileInfo
        prefix = fileInfo ~ /Web\/P/ ? "webp:" : ""
        conCmd = conCmd" "prefix tmpDir"/page_"i".jpg"
    }
    conCmd = conCmd" \""title".pdf\" 2>/dev/null"
    system(conCmd)
    system("rm -r "tmpDir)
}
